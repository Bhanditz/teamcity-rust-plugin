buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.9.1'
    }
}

ext {
    versionNumber = System.getenv('versionNumber') != null ? System.getenv('versionNumber') : 'SNAPSHOT'
    projectIds = ['group': 'teamcity-rust-plugin', 'version': versionNumber, 'artifact': 'rust-plugin.zip']

    teamcityVersion = project.hasProperty('teamcityVersion') ? project.getProperty('teamcityVersion') : '9.0'

    teamcityDir = project.hasProperty('teamcityDir') ? project.getProperty('teamcityDir') : "$rootDir/servers/TeamCity-${teamcityVersion}"
    teamcityDataDir = "$rootDir/data/" + (teamcityVersion =~ /(\d+\.\d+).*/)[0][1]

    teamcityJavaHome = System.properties['java.home']
}

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    test.useTestNG()
    jar.version = null
}

project(':plugin-rust-agent') {
    apply plugin: 'com.github.rodm.teamcity-agent'

    teamcity {
        version = teamcityVersion
        descriptor {
            pluginDeployment {
                useSeparateClassloader = true
            }
        }
    }

    dependencies {
        compile project(':plugin-rust-common')
        provided "org.jetbrains.teamcity.internal:agent:${teamcityVersion}"
        testCompile 'org.testng:testng:6.8'
    }
    agentPlugin.version = null
}

project(':plugin-rust-common') {
    apply plugin: 'com.github.rodm.teamcity-common'

    teamcity {
        version = teamcityVersion
    }
}

project(':plugin-rust-server') {
    apply plugin: 'com.github.rodm.teamcity-server'

    teamcity {
        version = teamcityVersion

        server {
            descriptor = project.file('teamcity-plugin.xml')
            tokens = [Plugin_Version: project.version]

            environments {
                teamcity9 {
                    version = teamcityVersion
                    homeDir = file(teamcityDir)
                    dataDir = file(teamcityDataDir)
                    javaHome = file(teamcityJavaHome)
                    serverOptions = '-Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5500 ' +
                            '-Dteamcity.oauthConnections.enabled=true ' +
                            '-Dteamcity.development.mode=true ' +
                            '-Dteamcity.development.shadowCopyClasses=true'
                }
            }
        }
    }

    dependencies {
        compile project(':plugin-rust-common')
        provided 'javax.servlet:jstl:1.1.2'

        agent project(path: ':plugin-rust-agent', configuration: 'plugin')
    }

    serverPlugin.version = null
}

apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = false
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}